---
id: "stream-management"
sidebar_position: 4
title: "Stream Management"
---

The "Stream Management" series will guide you through interacting with Flow, showing how to deposit, withdraw, adjust
rate per second, pause, restart, refund and void.

Before diving in, please note the following:

1. We assume you are already familiar with [creating streams](./03-create-stream.mdx).
2. We also assume that the stream management contract is authorized to invoke each respective function. To learn more
   about access control in Flow, see the [Access Control](/reference/flow/access-control) guide.

# Set up your contract

Declare the Solidity version used to compile the contract:

```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity >=0.8.22;
```

Import the relevant symbols from `@sablier/flow` and `@prb/math`:

```solidity
import { ud21x18 } from "@prb/math/src/UD21x18.sol";
import { ud60x18 } from "@prb/math/src/UD60x18.sol";
import { Broker, ISablierFlow } from "@sablier/flow/src/interfaces/ISablierFlow.sol";
```

Declare the contract and add the Flow contract as constant:

```solidity
contract FlowStreamManager {
    ISablierFlow public constant FLOW = ISablierFlow(0x5Ae8c13f6Ae094887322012425b34b0919097d8A);
}
```

In the code above, the contract address is hard-coded for demonstration purposes. However, in production, you would
likely use input parameters to allow flexibility in changing the addresses.

Also, this address is deployed on Sepolia. If you need to work with a different chain, the Sablier addresses can be
obtained from the [Deployment Addresses](/guides/flow/deployments) page.

## Deposit

Deposit into streams means adding funds to the stream, which will be distributed to the recipient according to the rate
per second.

There are three deposit functions:

1. [`deposit`](/reference/flow/contracts/contract.SablierFlow#deposit): deposits a specific amount of tokens.
2. [`depositAndPause`](/reference/flow/contracts/contract.SablierFlow#depositandpause): deposits and amount of tokens
   and pauses the stream.
3. [`depositViaBroker`](/reference/flow/contracts/contract.SablierFlow#depositviabroker): deposits an amount of tokens
   and also transfers a fee to a [broker](/concepts/fees#broker-fee).

```solidity
function deposit(uint256 streamId, uint256 amount) external {
    FLOW.deposit(streamId, amount);
}

function depositAndPause(uint256 streamId) external {
    FLOW.depositAndPause(streamId, 3.14159e18);
}

function depositViaBroker(uint256 streamId) external {
    Broker memory broker = Broker({ account: address(0xDEAD), fee: ud60x18(0.0001e18) });

    FLOW.depositViaBroker({
        streamId: streamId,
        totalAmount: 3.14159e18,
        sender: msg.sender,
        recipient: address(0xCAFE),
        broker: broker
    });
}
```

## Withdraw

Withdrawing from streams means claiming the tokens that have become due to the recipient, who has the option to direct
the withdrawal to an alternative address of their choice.

There are two withdrawal functions:

1. [`withdraw`](/reference/flow/contracts/contract.SablierFlow#withdraw): withdraws a specific amount of tokens.
2. [`withdrawMax`](/reference/flow/contracts/contract.SablierFlow#withdrawmax): withdraws the maximum withdrawable
   amount of tokens.

```solidity
function withdraw(uint256 streamId) external {
    FLOW.withdraw({ streamId: streamId, to: address(0xCAFE), amount: 2.71828e18 });
}

function withdrawMax(uint256 streamId) external {
    FLOW.withdrawMax({ streamId: streamId, to: address(0xCAFE) });
}
```

## Adjust Rate Per Second

Adjusting the rate per second means changing the amount of tokens that the stream will accrue every second.

```solidity
function adjustRatePerSecond(uint256 streamId) external {
    FLOW.adjustRatePerSecond({ streamId: streamId, newRatePerSecond: ud21x18(0.0001e18) });
}
```

## Pause

Pausing a stream means stopping the accrual of funds to the recipient.

```solidity
function pause(uint256 streamId) external {
    FLOW.pause(streamId);
}
```

## Restart

There are two restart functions:

1. [`restart`](/reference/flow/contracts/contract.SablierFlow#restart): restarts a stream.
2. [`restartAndDeposit`](/reference/flow/contracts/contract.SablierFlow#restartanddeposit): restarts a stream and
   deposits an amount of tokens.

```solidity
function restart(uint256 streamId) external {
    FLOW.restart({ streamId: streamId, ratePerSecond: ud21x18(0.0001e18) });
}

function restartAndDeposit(uint256 streamId) external {
    FLOW.restartAndDeposit({ streamId: streamId, ratePerSecond: ud21x18(0.0001e18), amount: 2.71828e18 });
}
```

## Refund

There are three refund functions:

1. [`refund`](/reference/flow/contracts/contract.SablierFlow#refund): refunds a specific amount of tokens.
2. [`refundAndPause`](/reference/flow/contracts/contract.SablierFlow#refundandpause): refunds an amount of tokens and
   pauses the stream.
3. [`refundMax`](/reference/flow/contracts/contract.SablierFlow#refundmax): refunds the maximum refundable amount of
   tokens.

```solidity
function refund(uint256 streamId) external {
    FLOW.refund({ streamId: streamId, amount: 1.61803e18 });
}

function refundAndPause(uint256 streamId) external {
    FLOW.refundAndPause({ streamId: streamId, amount: 1.61803e18 });
}

function refundMax(uint256 streamId) external {
    FLOW.refundMax(streamId);
}
```

## Void

Voiding a stream means stopping it forever from accruing funds.

```solidity
function void(uint256 streamId) external {
    FLOW.void(streamId);
}
```

## The complete Flow stream manager contract

Below you can see the complete `FlowStreamManager` contract:

```solidity reference title="Flow Stream Creator"
https://github.com/sablier-labs/examples/blob/main/flow/FlowStreamManager.sol
```

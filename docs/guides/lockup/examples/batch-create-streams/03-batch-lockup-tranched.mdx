---
id: "batch-lockup-tranched"
sidebar_position: 3
title: "Batch Lockup Tranched"
---

import AdmonitionSimpleCode from "@site/docs/snippets/AdmonitionSimpleCode.mdx";
import BatchCommonSteps from "@site/docs/snippets/BatchCommonSteps.mdx";
import ConstantsComment from "@site/docs/snippets/ConstantsComment.mdx";

# Create a Batch of Lockup Tranched Streams

In this guide, we will show you how to programmatically batch create tranched streams via the Lockup's
[Batch](/reference/lockup/periphery/contract.SablierV2BatchLockup) contract.

This guide assumes that you have already gone through the [Protocol Concepts](/concepts/streaming) section.

:::note

This guide interacts with the Periphery contract.

:::

<AdmonitionSimpleCode />

## Set up a contract

Declare the Solidity version used to compile the contract:

```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity >=0.8.22;
```

Now, import the relevant symbols from `@sablier/v2-core` and `@sablier/v2-periphery`:

```solidity
import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { ud60x18 } from "@prb/math/src/UD60x18.sol";
import { ISablierV2LockupTranched } from "@sablier/v2-core/src/interfaces/ISablierV2LockupTranched.sol";
import { Broker, LockupTranched } from "@sablier/v2-core/src/types/DataTypes.sol";
import { ISablierV2BatchLockup } from "@sablier/v2-periphery/src/interfaces/ISablierV2BatchLockup.sol";
import { BatchLockup } from "@sablier/v2-periphery/src/types/DataTypes.sol";
```

Create a contract called `BatchLTStreamCreator`, and declare a constant `DAI` of type `IERC20`, a constant
`LOCKUP_TRANCHED` of type `ISablierV2LockupTranched`, and a constant `BATCH_LOCKUP` of type `ISablierV2BatchLockup`:

```solidity
contract BatchLTStreamCreator {
    IERC20 public constant DAI = IERC20(0x68194a729C2450ad26072b3D33ADaCbcef39D574);
    ISablierV2LockupTranched public constant LOCKUP_TRANCHED =
        ISablierV2LockupTranched(0x3a1beA13A8C24c0EA2b8fAE91E4b2762A59D7aF5);
    ISablierV2BatchLockup public constant BATCH_LOCKUP = ISablierV2BatchLockup(0x04A9c14b7a000640419aD5515Db4eF4172C00E31);
}
```

<ConstantsComment />

## Batch create functions

There are two batch create functions for the Lockup Tranched contract:

- [`createWithDurationsLT`](/reference/lockup/periphery/contract.SablierV2BatchLockup#createwithdurationslt)
- [`createWithTimestampsLT`](/reference/lockup/periphery/contract.SablierV2BatchLockup#createwithtimestampslt)

Which one you choose depends upon your use case. In this guide, we will use `createWithTimestampsLT`.

<BatchCommonSteps />

## Stream Parameters

Given that we declared a `batchSize` of two, we need to define two
[BatchLockup.CreateWithTimestampsLT](/reference/lockup/periphery/types/library.BatchLockup#createwithtimestampslt)
structs:

```solidity
// Declare the first stream in the batch
BatchLockup.CreateWithTimestampsLT memory stream0;
stream0.sender = address(0xABCD); // The sender to stream the assets, he will be able to cancel the stream
stream0.recipient = address(0xCAFE); // The recipient of the streamed assets
stream0.totalAmount = perStreamAmount; // The total amount of each stream, inclusive of all fees
stream0.cancelable = true; // Whether the stream will be cancelable or not
stream0.transferable = false; // Whether the recipient can transfer the NFT or not
stream0.startTime = uint40(block.timestamp); // Set the start time to block timestamp
// Declare some dummy tranches
stream0.tranches = new LockupTranched.Tranche[](2);
stream0.tranches[0] = LockupTranched.Tranche({
    amount: uint128(perStreamAmount / 2),
    timestamp: uint40(block.timestamp + 1 weeks)
});
stream0.tranches[1] = LockupTranched.Tranche({
    amount: uint128(perStreamAmount - stream0.tranches[0].amount),
    timestamp: uint40(block.timestamp + 24 weeks)
});
stream0.broker = Broker(address(0), ud60x18(0)); // Optional parameter left undefined
```

To add some variety, we will change the parameters of the second stream:

```solidity
BatchLockup.CreateWithTimestampsLT memory stream1;
stream1.sender = address(0xABCD); // The sender to stream the assets, he will be able to cancel the stream
stream1.recipient = address(0xBEEF); // The recipient of the streamed assets
stream1.totalAmount = uint128(perStreamAmount); // The total amount of each stream, inclusive of all fees
stream1.cancelable = false; // Whether the stream will be cancelable or not
stream1.transferable = false; // Whether the recipient can transfer the NFT or not
stream1.startTime = uint40(block.timestamp); // Set the start time to block timestamp
// Declare some dummy tranches
stream1.tranches = new LockupTranched.Tranche[](2);
stream1.tranches[0] = LockupTranched.Tranche({
    amount: uint128(perStreamAmount / 4),
    timestamp: uint40(block.timestamp + 4 weeks)
});
stream1.tranches[1] = LockupTranched.Tranche({
    amount: uint128(perStreamAmount - stream1.tranches[0].amount),
    timestamp: uint40(block.timestamp + 24 weeks)
});
stream1.broker = Broker(address(0), ud60x18(0)); // Optional parameter left undefined
```

Once both structs are declared, the batch array has to be filled:

```solidity
// Fill the batch array
BatchLockup.CreateWithTimestampsLT[] memory batch = new BatchLockup.CreateWithTimestampsLT[](batchSize);
batch[0] = stream0;
batch[1] = stream1;
```

## Invoke the batch create function

With all parameters set, we can now call the `createWithTimestampsLT` function, and assign the ids of the newly created
streams to the array:

```solidity
streamIds = BATCH_LOCKUP.createWithTimestampsLT(LOCKUP_TRANCHED, DAI, batch);
```

## The complete Batch Lockup Tranched stream creator contract

Below you can see the complete functioning code: a contract that batch creates Lockup Tranched streams using Lockup's
`Batch` that start at `block.timestamp`. You can access the code on GitHub through this
[link](https://github.com/sablier-labs/examples/blob/main/lockup/periphery/BatchLTStreamCreator.sol).

```solidity reference title="Batch Lockup Tranched stream creator"
https://github.com/sablier-labs/examples/blob/main/lockup/periphery/BatchLTStreamCreator.sol
```

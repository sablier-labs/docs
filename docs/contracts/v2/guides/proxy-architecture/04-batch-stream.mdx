---
id: "batchstream"
sidebar_position: 4
title: "Batch stream"
---

import AdmonitionSimpleCode from "@site/docs/snippets/AdmonitionSimpleCode.mdx";

In this guide, we will show you how to batch create streams via the Proxy and Sablier's Proxy Target programmatically.

:::note

This guide assumes that you have already read Create
[Single Stream](/contracts/v2/guides/proxy-architecture/singlestream) section.

:::

<AdmonitionSimpleCode />

Batch create as the name suggests, involves creating multiple streams within a transaction. Since we are dealing with
multiple streams, we will not use the core create structs, instead, we declared a
[Batch](/contracts/v2/reference/periphery/types/library.Batch) library. Using the same structs would have been
inefficient due to multiple ERC-20 transfers from the Proxy owner to the Proxy, so we pass the token as a parameter in
the function, without encapsulating it in each struct of the array.

## Batch create functions

There are four batch create functions in the Proxy Target contract, two for each Lockup contract:

Lockup Linear:

- [`batchCreateWithDurations`](/contracts/v2/reference/periphery/contract.SablierV2ProxyTarget#batchcreatewithdurations)
- [`batchCreateWithRange`](/contracts/v2/reference/periphery/contract.SablierV2ProxyTarget#batchcreatewithrange)

Lockup Dynamic:

- [`batchCreateWithDeltas`](/contracts/v2/reference/periphery/contract.SablierV2ProxyTarget#batchcreatewithdeltas)
- [`batchCreateWithMilestones`](/contracts/v2/reference/periphery/contract.SablierV2ProxyTarget#batchcreatewithmilestones)

Let's start with a Lockup Linear example:

## Set up a contract

Declare the Solidity version used to compile the contract:

```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity >=0.8.19;
```

As mentioned in the [Overview](/contracts/v2/guides/proxy-architecture/overview), we use `Permit2` for all token
interactions. In our example we will deal with a contract creator, and contracts can't sign transactions. Having that
said, we will need an implementation of the [EIP-1271](https://eips.ethereum.org/EIPS/eip-1271) standard.

Let's declare a dummy contract `ERC1271` which returns the magic value (the function signature of `isValidSignature`):

```solidity
/// @notice Permit2 uses this ERC to validate contract signatures.
contract ERC1271 {
    function isValidSignature(bytes32, /* hash */ bytes memory /* signature */ ) public pure returns (bytes4) {
        return this.isValidSignature.selector;
    }
}
```

Import the relevant symbols from `@sablier/v2-core` and `@sablier/v2-periphery`:

```solidity
import { ISablierV2LockupLinear } from "@sablier/v2-core/interfaces/ISablierV2LockupLinear.sol";
import { LockupLinear } from "@sablier/v2-core/types/DataTypes.sol";
import { ud60x18 } from "@sablier/v2-core/types/Math.sol";
import { IERC20 } from "@sablier/v2-core/types/Tokens.sol";
import { ISablierV2ProxyTarget } from "@sablier/v2-periphery/interfaces/ISablierV2ProxyTarget.sol";
import { Batch, Broker } from "@sablier/v2-periphery/types/DataTypes.sol";
import { IAllowanceTransfer, Permit2Params } from "@sablier/v2-periphery/types/Permit2.sol";
import { IPRBProxy, IPRBProxyRegistry } from "@sablier/v2-periphery/types/Proxy.sol";
```

Create a contract called `BatchLockupLinearStreamCreator` that inherits `ERC1271`, and declare a constant `DAI` of type
`IERC20`, a constant `PERMIT2` of type `IAllowanceTransfer`, a constant `PROXY_REGISTRY` of type `IPRBProxyRegistry`, an
immutable variable `lockupLinear` of type `ISablierV2LockupLinear` and an immutable variable `proxyTarget` of type
`ISablierV2ProxyTarget`:

```solidity
contract BatchLockupLinearStreamCreator is ERC1271 {
    IERC20 public constant DAI = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
    IAllowanceTransfer public constant PERMIT2 = IAllowanceTransfer(0x000000000022D473030F116dDEE9F6B43aC78BA3);
    IPRBProxyRegistry public constant PROXY_REGISTRY = IPRBProxyRegistry(0x584009E9eDe26e212182c9745F5c000191296a78);
    ISablierV2LockupLinear public immutable lockupLinear;
    ISablierV2ProxyTarget public immutable proxyTarget;
}
```

In the code above, the address of the [DAI](https://makerdao.com) stablecoin, Permit2 and Proxy Registry are hard-coded.
However, you would likely use an input parameter to allow the contract to change the assets it interacts with on a per
transaction basis. Keep in mind that Permit2 is deployed at the same
[address](https://docs.uniswap.org/contracts/v3/reference/deployments) on all [chains](/contracts/v2/deployments) where
Sablier V2 is live, also Proxy Registry is deployed at the same address across these
[chains](https://prbproxy.com/deployments).

## Initialization

To initialize the Lockup Linear contract and the Proxy Target, you first need to grab their addresses from the
[Deployment Addresses](/contracts/v2/deployments) page. Once you have obtained them, pass them to the constructor of the
creator contract:

```solidity
constructor(ISablierV2LockupLinear lockupLinear_, ISablierV2ProxyTarget proxyTarget_) {
    lockupLinear = lockupLinear_;
    proxyTarget = proxyTarget_;
}
```

## Function definition

Define a function called `batchCreateLockupLinearStream` which takes a parameter `perStreamAmount`, and which returns an
array of ids for the created streams:

```solidity
function batchCreateLockupLinearStream(uint256 perStreamAmount) public returns (uint256[] memory streamIds) {
    //
}
```

## Proxy Steps

We need to deploy a Proxy instance for the creator contract if it doesn't exists:

```solidity
// Get the proxy for this contract and deploy it if it doesn't exist
IPRBProxy proxy = PROXY_REGISTRY.getProxy({ owner: address(this) });
if (address(proxy) == address(0)) {
    proxy = PROXY_REGISTRY.deployFor(address(this));
}
```

Next, we need to declare a batch size, which is needed to calculate the transfer amount:

```solidity
// Create a batch of two streams
uint256 batchSize = 2;

// Calculate the combined amount of DAI assets to transfer to this contract
uint256 transferAmount = perStreamAmount * batchSize;
```

## ERC-20 Steps

To batch create streams, the caller must approve the creator contract to pull the tokens from the calling address's
account. Then, if the allowance is less than the transferred amount, we have to also approve the Permit2 contract. To
fully benefit from Permit2, we should do a maximum approval:

```solidity
// Transfer the provided amount of DAI tokens to this contract
DAI.transferFrom(msg.sender, address(this), totalAmount);

// Approve the Permit2 contract to spend DAI
uint256 allowance = DAI.allowance(address(this), address(PERMIT2));
if (allowance < totalAmount) {
    DAI.approve({ spender: address(PERMIT2), amount: type(uint256).max });
}
```

## Permit2 Steps

We are going to declare the Permit2 variables needed in the
[Proxy Target](/contracts/v2/reference/periphery/types/struct.Permit2Params).

```solidity
// Set up Permit2. See the full documentation at https://github.com/Uniswap/permit2
IAllowanceTransfer.PermitDetails memory permitDetails;
permitDetails.token = address(DAI);
permitDetails.amount = uint160(totalAmount);
permitDetails.expiration = type(uint48).max; // maximum expiration possible
(,, permitDetails.nonce) = PERMIT2.allowance({ user: address(this), token: address(DAI), spender: address(proxy) });

IAllowanceTransfer.PermitSingle memory permitSingle;
permitSingle.details = permitDetails;
permitSingle.spender = address(proxy); // the proxy will be the spender
permitSingle.sigDeadline = type(uint48).max; // same deadline as expiration

// Declare the Permit2 params needed by Sablier
Permit2Params memory permit2Params;
permit2Params.permitSingle = permitSingle;
permit2Params.signature = bytes(""); // dummy signature
```

In the code above, we passed a null `bytes` because contracts can't sign transactions. If you were to use an EOA, a
valid signature would be required.

You can find the documentation for the Permit2 structs on Uniswap's
[website](https://docs.uniswap.org/contracts/permit2/reference/allowance-transfer).

## Parameters

Given that we declared above a `batchSize` of two, we need to also define two
[Batch.CreateWithDurations](/contracts/v2/reference/periphery/types/library.Batch#createwithdurations) structs, let's
use the same parameters we've previously declared in the
[Create Stream](/contracts/v2/guides/create-stream/lockup-linear#parameters) section:

```solidity
// Declare the first stream in the batch
Batch.CreateWithDurations memory stream0;
stream0.sender = address(proxy); // The sender will be able to cancel the stream
stream0.recipient = address(0xcafe); // The recipient of the streamed assets
stream0.totalAmount = uint128(perStreamAmount); // The total amount of each stream, inclusive of all fees
stream0.cancelable = true; // Whether the stream will be cancelable or not
stream0.durations = LockupLinear.Durations({
    cliff: 4 weeks, // Assets will be unlocked only after 4 weeks
    total: 52 weeks // Setting a total duration of ~1 year
    });
stream0.broker = Broker(address(0), ud60x18(0)); // Optional parameter left undefined
```

For the second struct we will change the parameters:

```solidity
// Declare the second stream in the batch
Batch.CreateWithDurations memory stream1;
stream1.sender = address(proxy); // The sender will be able to cancel the stream
stream1.recipient = address(0xbeef); // The recipient of the streamed assets
stream1.totalAmount = uint128(perStreamAmount); // The total amount of each stream, inclusive of all fees
stream1.cancelable = false; // Whether the stream will be cancelable or not
stream1.durations = LockupLinear.Durations({
    cliff: 1 weeks, // Assets will be unlocked only after 1 week
    total: 26 weeks // Setting a total duration of ~6 months
});
stream1.broker = Broker(address(0), ud60x18(0)); // Optional parameter left undefined
```

After we declared both structs, we have to fill batch array:

```solidity
// Fill the batch param
Batch.CreateWithDurations[] memory batch = new Batch.CreateWithDurations[](batchSize);
batch[0] = stream0;
batch[1] = stream1;
```

## Batch create streams

Due to the nature of `delegatecall`, it's necessary to encode the parameters declared above into `bytes`:

```solidity
bytes memory data = abi.encodeCall(proxyTarget.batchCreateWithDurations, (lockupLinear, DAI, batch, permit2Params));
```

Once this set-up is complete, we can batch create streams via the Proxy and Sablier's Proxy Target:

```solidity
bytes memory response = proxy.execute(address(proxyTarget), data);
```

To return the `streamIds` we need to decode the response to `uint256[]`:

```solidity
streamIds = abi.decode(response, (uint256[]));
```

## The complete Batch Lockup Linear stream creator contract

Below you can see the complete functioning code: a contract that batch creates Lockup Linear streams via the Proxy and
Sablier's Proxy Target that start at `block.timestamp`. You can access the code on GitHub through this
[link](https://github.com/sablier-labs/examples/blob/main/v2/periphery/BatchLockupLinearStreamCreator.sol).

```solidity reference title="Batch Lockup Linear stream creator"
https://github.com/sablier-labs/examples/blob/main/v2/periphery/BatchLockupLinearStreamCreator.sol
```

<hr style={{ borderTop: "1px solid black" }} />

For the Lockup Dynamic example, we can apply the same steps as above, we won't detail each step again here.

The difference here is that we declared different batch create structs:

```solidity
// Declare the first stream in the batch
Batch.CreateWithMilestones memory stream0;
// ...
// Declare the second stream in the batch
Batch.CreateWithMilestones memory stream1;
// ...
// Fill the batch array
Batch.CreateWithMilestones[] memory batch = new Batch.CreateWithMilestones[](batchSize);
batch[0] = stream0;
batch[1] = stream1;
```

## The complete Batch Lockup Dynamic stream creator contract

Below you can see the complete functioning code: a contract that batch creates Lockup Dynamic streams via the Proxy and
Sablier's Proxy Target with predefined segment milestones. You can access the code on GitHub through this
[link](https://github.com/sablier-labs/examples/blob/main/v2/periphery/BatchLockupDynamicStreamCreator.sol).

```solidity reference title="Batch Lockup Dynamic stream creator"
https://github.com/sablier-labs/examples/blob/main/v2/periphery/BatchLockupDynamicStreamCreator.sol
```

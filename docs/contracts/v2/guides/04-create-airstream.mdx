---
id: "create-airstream"
sidebar_position: 4
title: "Create Airstream"
---

import AdmonitionSimpleCode from "@site/docs/snippets/AdmonitionSimpleCode.mdx";
import ConstantsComment from "@site/docs/snippets/ConstantsComment.mdx";

# Create an Airstream

In this guide, we will show you how to programmatically create an airstream via the Sablier's
[Merkle Factory](http://localhost:3000/contracts/v2/reference/periphery/contract.SablierV2MerkleLockupFactory) contract.

This guide assumes that you have already gone through the
[Protocol Concepts](http://localhost:3000/concepts/protocol/airstreams) section.

:::note

This guide interacts with the Periphery contract.

:::

<AdmonitionSimpleCode />

## Set up a contract

Declare the Solidity version used to compile the contract:

```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity >=0.8.22;
```

Now, import the relevant symbols from `@sablier/v2-core` and `@sablier/v2-periphery`:

```solidity
import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { ISablierV2LockupLinear } from "@sablier/v2-core/src/interfaces/ISablierV2LockupLinear.sol";
import { LockupLinear } from "@sablier/v2-core/src/types/DataTypes.sol";
import { ISablierV2MerkleLL } from "@sablier/v2-periphery/src/interfaces/ISablierV2MerkleLL.sol";
import { ISablierV2MerkleLockupFactory } from "@sablier/v2-periphery/src/interfaces/ISablierV2MerkleLockupFactory.sol";
import { MerkleLockup } from "@sablier/v2-periphery/src/types/DataTypes.sol";
```

Create a contract called `AirstreamCreator`, and declare a constant `DAI` of type `IERC20`, a constant `LOCKUP_LINEAR`
of type `ISablierV2LockupLinear` and a constant `FACTORY` of type `ISablierV2MerkleLockupFactory`.

```solidity
contract AirstreamCreator {
    IERC20 public constant DAI = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
    ISablierV2LockupLinear public constant LOCKUP_LINEAR =
        ISablierV2LockupLinear(0xAFb979d9afAd1aD27C5eFf4E27226E3AB9e5dCC9);
    ISablierV2MerkleLockupFactory public constant FACTORY = ISablierV2MerkleLockupFactory(0xEa07DdBBeA804E7fe66b958329F8Fa5cDA95Bd55);
}
```

<ConstantsComment />

## Airstream create functions

There are two Airstream create functions available through factory:

- [`createMerkleLL`](/contracts/v2/reference/periphery/contract.SablierV2MerkleLockupFactory#createmerklell): This uses
  Lockup Linear streams to create Airstream campaign.
- [`createMerkleLT`](/contracts/v2/reference/periphery/contract.SablierV2MerkleLockupFactory#createmerklelt): This uses
  Lockup Tranched streams to create Airstream campaign.

Which one you choose depends upon your use case. In this guide, we will use `createMerkleLL`.

## Function definition

Define a function called `createLLAirstream` that and returns the address of newly deployed Merkle Lockup contract.

```solidity
function createLLAirstream() public returns (ISablierV2MerkleLL merkleLL) {
  // ...
}
```

## Parameters

MerkleLockupFactory uses
[MerkleLockup.ConstructorParams](/contracts/v2/reference/periphery/types/library.MerkleLockup#constructorparams) as the
shared struct between `createMerkleLL` and `createMerkleLT` functions.

```solidity
MerkleLockup.ConstructorParams memory baseParams;
```

Let's review each parameter of the shared struct in detail.

### Asset

The contract address of the ERC-20 asset that you want to airdrop to your recipients. In this example, we will use DAI.

```solidity
baseParams.asset = DAI;
```

### Cancelable

Boolean that indicates whether the stream will be cancelable or not after it has been claimed.

```solidity
baseParams.cancelable = true;
```

### Expiration

The unix timestamp indicating the expiration of the campaign. Once this time has been passed, users will no longer be
able to claim their airdrop. And you will be able to
[clawback](/contracts/v2/reference/periphery/abstracts/abstract.SablierV2MerkleLockup#clawback) any unclaimed assets
from the campaign.

```solidity
baseParams.expiration = uint40(block.timestamp + 12 weeks);
```

### Initial Admin

This is the initial admin of the Airstream campaign. When a recipient claims his airdrop, a Sablier stream is created
with this admin as the sender of the stream. Another role of admin is to clawback unclaimed assets from the campaign
post expiry and during grace period.

```solidity
baseParams.initialAdmin = address(0xBeeF);
```

### IPFS CID

This is the content identifier (CID) for indexing the contract on IPFS. This is where we store addresses of the Airdrop
recipients and their claim amount.

```solidity
baseParams.ipfsCID = "QmT5NvUtoM5nWFfrQdVrFtvGfKFmG7AHE8P34isapyhCxX";
```

### Merkle Root

Airstream campaign uses Merkle tree data structure to store airdrop data on-chain. As a result, you only pay the gas fee
to create the contract and storing the Merkle root on the contract. Airdrop recipients can then call the contract on
their own to claim their airdrop.

If you want to create the Merkle root programatically, you can follow [our guide on Merkle API](/api/merkle-api/intro).

```solidity
baseParams.merkleRoot = 0x4e07408562bedb8b60ce05c1decfe3ad16b722309875f562c03d02d7aaacb123;
```

### Name

The name of the campaign.

```solidity
baseParams.name = "My First Campaign";
```

### Transferable

Boolean that indicates whether the stream will be transferable or not.

```solidity
baseParams.transferable = true;
```

Now we have the `baseParams` ready, it's time to setup rest of the input parameters.

### Stream Duration

For Lockup Linear, this is the struct containing (i) cliff period duration and (ii) total stream duration, both in
seconds.

```solidity
LockupLinear.Durations memory streamDurations;
streamDurations = LockupLinear.Durations({
    cliff: 4 weeks,
    total: 52 weeks
});
```

The stream duration applies only after the Airstream has been claimed by the recipient. Therefore, users claiming
airstreams at different times will have their assets vested at different intervals. For example, user B claims 2 days
after user A, then the stream for user B will end 2 days after the stream for user A ends.

### Aggregate Amount

This is the total amount of assets you want to airdrop to your users, denoted in units of the asset's decimals. Let say
you want to airdrop 100M tokens of DAI, then it would be $100M*10^{18}$.

```solidity
uint256 aggregateAmount = 100000000e18;
```

### Recipient Count

The total number of addresses you want to airdrop your tokens too.

```solidity
uint256 recipientCount = 10000;
```

## Invoke the create function

With all parameters set, we can now call the `createMerkleLL` function, and assign the address of the newly created
merkle lockkup campaign to a variable:

```solidity
merkleLL = FACTORY.createMerkleLL(baseParams, LOCKUP_LINEAR, streamDurations, aggregateAmount, recipientCount);
```

## The complete Airstream creator contract

Below you can see the complete functioning code: a contract that creates an airstream campaign with Lockup Linear
stream. You can access the code on GitHub through
[this link](https://github.com/sablier-labs/examples/blob/main/v2/periphery/AirstreamCreator.sol).

```solidity reference title="Airstream Creator"
https://github.com/sablier-labs/examples/blob/main/v2/periphery/AirstreamCreator.sol
```

---
id: "lockup-dynamic"
sidebar_position: 2
title: "Lockup Dynamic"
---

import AdmonitionSimpleCode from "@site/docs/snippets/AdmonitionSimpleCode.mdx";
import ConstantsComment from "@site/docs/snippets/ConstantsComment.mdx";
import ParamAsset from "@site/docs/snippets/ParamAsset.mdx";
import ParamBroker from "@site/docs/snippets/ParamBroker.mdx";
import ParamCancelable from "@site/docs/snippets/ParamCancelable.mdx";
import ParamRecipient from "@site/docs/snippets/ParamRecipient.mdx";
import ParamSender from "@site/docs/snippets/ParamSender.mdx";
import ParamTotalAmount from "@site/docs/snippets/ParamTotalAmount.mdx";

# Create a Lockup Dynamic stream

Lockup Dynamic are streams with custom streaming curves. In this guide, we will show you how to programmatically create
a Lockup Dynamic stream.

This guide assumes that you have already gone through the [Protocol Concepts](/concepts/protocol/streaming) section.

:::note

This guide interacts with the Core contracts directly.

:::

<AdmonitionSimpleCode />

## Set up a contract

Declare the Solidity version used to compile the contract:

```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity >=0.8.19;
```

Import the relevant symbols from `@sablier/v2-core`:

```solidity
import { ISablierV2LockupDynamic } from "@sablier/v2-core/src/interfaces/ISablierV2LockupDynamic.sol";
import { Broker, LockupDynamic } from "@sablier/v2-core/src/types/DataTypes.sol";
import { ud2x18, ud60x18 } from "@sablier/v2-core/src/types/Math.sol";
import { IERC20 } from "@sablier/v2-core/src/types/Tokens.sol";
```

Create a contract called `LockupDynamicStreamCreator`, and declare a constant `DAI` of type `IERC20` and a constant
`LOCKUP_DYNAMIC` of type `ISablierV2LockupDynamic`:

```solidity
contract LockupDynamicStreamCreator {
    IERC20 public constant DAI = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
    ISablierV2LockupDynamic public constant LOCKUP_Dynamic =
        ISablierV2LockupDynamic(0x7CC7e125d83A581ff438608490Cc0f7bDff79127);
}
```

<ConstantsComment />

## Create functions

There are two create functions in the Lockup Dynamic contract:

- `createWithMilestones`: uses specific start and end times, as well as a predefined list of segment milestones
- `createWithDeltas`: sets the start time to
  [`block.timestamp`](https://docs.soliditylang.org/en/v0.8.19/cheatsheet.html#global-variables), and calculates the end
  time and the segment milestones based on the provided deltas

Which one you choose depends upon your use case. In this guide, we will use `createWithMilestones`.

## Function definition

Define a function called `createStream` which takes two parameters, `amount0` and `amount1`, and which returns the id of
the created stream:

```solidity
function createStream(uint128 amount0, uint128 amount1) public returns (uint256 streamId) {
  // ...
}
```

Next, sum up the `amount0` and `amount1` parameters to get the total amount of the stream, which will be needed in many
of the steps below:

```solidity
uint256 totalAmount = amount0 + amount1;
```

## ERC-20 steps

To create a stream, the caller must approve the creator contract to pull the tokens from the calling address's account.
Then, we have to also approve the Sablier contract to pull the assets that the creator contract will be in possession of
after they are transferred from the calling address (you):

```solidity
// Transfer the provided amount of DAI tokens to this contract
DAI.transferFrom(msg.sender, address(this), totalAmount);

// Approve the Sablier contract to spend DAI
DAI.approve(address(LOCKUP_DYNAMIC), totalAmount);
```

For more guidance on how to approve and transfer ERC-20 assets, see
[this article](https://ethereum.org/en/developers/docs/standards/tokens/erc-20/) on the Ethereum website.

## Parameters

Sablier uses structs to encode the parameters of its create functions. The struct associated with `createWithDurations`
is [`LockupDynamic.CreateWithMilestones`](/contracts/v2/reference/core/types/library.LockupDynamic), and it can be
initialized like this:

```solidity
LockupDynamic.CreateWithMilestones memory params;
```

Let's review each parameter in detail.

<ParamSender />

<ParamRecipient />

<ParamTotalAmount />

<ParamAsset />

<ParamCancelable />

<ParamBroker />

### Start time

The Unix timestamp for when the stream will start.

In this example, the start time is set to 100 seconds into the future:

```solidity
params.startTime = block.timestamp + 100 seconds;
```

### Segments

Segments are what the protocol uses to compose the custom streaming curve of a Lockup Dynamic stream. For a full
exposition of segments, see the [Segments](/concepts/protocol/segments) guide.

The term "milestones" in the function `createWithMilestones` refers to the splitting of the stream into several distinct
segments, with each segment characterized by a specific amount, exponent, and milestone. These segments are supplied to
the function in the form of an array containing
[`LockupDynamic.Segment`](/contracts/v2/reference/core/types/library.LockupDynamic) structs.

Let's define two dummy segments:

```solidity
params.segments = new LockupDynamic.Segment[](2);
params.segments[0] = LockupDynamic.Segment({
    amount: amount0,
    exponent: ud2x18(1e18),
    milestone: uint40(block.timestamp + 4 weeks)
});
params.segments[1] = (
    LockupDynamic.Segment({
        amount: amount1,
        exponent: ud2x18(3.14e18),
        milestone: uint40(block.timestamp + 52 weeks)
    })
);
```

In this example, the first segment (`amount0`) will stream much faster than the second segment (`amount1`), because the
exponents are different. As a rule of thumb: the higher the exponent, the slower the stream.

:::note

The segment milestones must be in ascending order.

:::

:::info

The `ud2x18` function wraps a basic integer to the `UD2x18` value type, which is part of the
[PRBMath](https://github.com/PaulRBerg/prb-math) library.

:::

## Invoke the create function

With all parameters set, we can now call the `createWithMilestones` function, and assign the id of the newly created
stream to a variable:

```solidity
streamId = LOCKUP_DYNAMIC.createWithMilestones(params);
```

## The complete Lockup Dynamic stream creator contract

Below you can see the complete functioning code: a contract that creates a Lockup Dynamic stream with predefined segment
milestones. You can access the code on GitHub through
[this link](https://github.com/sablier-labs/examples/blob/main/v2/core/LockupDynamicStreamCreator.sol).

```solidity reference title="Lockup Dynamic Stream Creator"
https://github.com/sablier-labs/examples/blob/main/v2/core/LockupDynamicStreamCreator.sol
```
